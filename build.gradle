buildscript {
    repositories {
//        mavenLocal()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.palantir.graal:gradle-graal:0.7.1"

    }
}


apply plugin: 'com.palantir.graal'

subprojects {

    apply plugin: "java"
    apply plugin: "scala"
    apply plugin: 'com.palantir.graal'
    apply plugin: 'maven-publish'

    group 'org.mule.weave.native'
    version nativeVersion

    compileScala {
        targetCompatibility = "11"
        sourceCompatibility = "11"
    }


    compileJava   {
        sourceCompatibility = '11'
        targetCompatibility = '11'
    }


    publishing {
        publications.all {
            pom.withXml {
                asNode().dependencies.'*'.findAll() {
                    it.scope.text() == 'runtime' && project.configurations.compile.allDependencies.find { dep ->
                        dep.name == it.artifactId.text()
                    }
                }.each { it.scope*.value = 'compile' }
            }
        }
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }
        repositories {
            maven {
                name "mule-releases"
                url "https://repository-master.mulesoft.org/nexus/content/repositories/${project.version.endsWith('-SNAPSHOT') ? 'snapshots/' : 'releases/'}"
            }
        }
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            name "mule-releases"
            url "http://repository.mulesoft.org/nexus/content/repositories/releases/"
        }
        maven {
            name "mule-snapshots"
            url "http://repository.mulesoft.org/nexus/content/repositories/snapshots/"
        }
        maven {
            name "jitpack.io"
            url "https://jitpack.io"
        }
    }


    dependencies {
        compile group: 'org.scala-lang', name: 'scala-library', version: scalaVersion
    }
}